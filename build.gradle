  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'maven'
  apply plugin: 'jacoco'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  version = '2.0.0'
  
  description = 'Grengine is an engine for running and embedding Groovy in a Java VM.'

  project.ext {
    pkg = 'jar'
  }
  
  ext {
    groovyVersion='2.5.5'
    junitVersion='5.3.2'
    hamcrestVersion='2.1'
    ivyVersion='2.4.0'
    commonsIoVersion='2.6'
  }

  jar {
    manifest {
      attributes 'Implementation-Title': 'grengine', 'Implementation-Version': version, 'provider': 'gradle'
    }
  }

  dependencies {
    compile "org.codehaus.groovy:groovy:$groovyVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
    testCompile "org.apache.ivy:ivy:$ivyVersion"
    testCompile "commons-io:commons-io:$commonsIoVersion"
  }

  project.group = 'ch.grengine'

  repositories {
    mavenCentral()
  }

  task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }
  
  javadoc {
    configure(options) {
      windowTitle = "Grengine - $project.version"
      header = 'Grengine'
      author = true
      links = [ 'https://docs.oracle.com/javase/8/docs/api/', 'http://docs.groovy-lang.org/docs/groovy-' + groovyVersion + '/html/api/' ]
      tags = [ 'grengine.scriptthrows:a:Throws (out of Groovy script):' ]
    }
  }

  task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  tasks.withType(JavaCompile) {
    // treat warnings as errors
    options.compilerArgs << "-Xlint:all" << "-Werror"
  }
  
  test {
    testLogging.showStandardStreams = true
    useJUnitPlatform()
  }
  
  artifacts {
    archives sourcesJar 
    archives javadocJar 
  }
  
  task jacoco(type:JacocoReport) {
    executionData test
    sourceSets sourceSets.main
  }
  
  task pom {
    doLast {
      pom {
        project {
          name 'grengine'
          description project.description
          url 'https://www.grengine.ch/'
          packaging project.ext.pkg
          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'https://www.apache.org/licenses/LICENSE-2.0'
              distribution 'repo'
            }
          }
          scm {
            url 'https://gitlab.com/jexler/grengine'
            connection 'https://gitlab.com/jexler/grengine.git'
            developerConnection 'https://gitlab.com/jexler/grengine.git'
          }
          developers {
            developer {
              id 'jexler'
              name 'Jex Jexler (whois jexler.net)'
              email 'jex@jexler.net'
            }
          }
        }
      }.writeTo("$buildDir/libs/${project.name}-${project.version}.pom")
    }
  }
